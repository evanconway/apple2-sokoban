0000 REM GAME START
0010 GR
0020 HOME
0021 LH = 8 : REM GAME LEVEL HEIGHT
0022 LW = 8 : REM GAME LEVEL WIDTH
0030 DIM LA(LH, LW) : REM DECLARE LEVEL ARRAY (HEIGHT, WIDTH)
0040 DIM WA(LH, LW) : REM DECLARE WORLD ARRAY (HEIGHT, WIDTH)
0050 DIM PP(2) : REM PLAYER POSITION (Y, X)
0060 RESTORE
0070 GOSUB 1000
0072 GOSUB 2000
0075 GOSUB 3000
0080 REM GAMEPLAY LOOP
0090 GET KY$ : REM GET USER INPUT
0100 IF KY$ = "Q" THEN END
110 IF KY$ = "W" THEN PP(1) = PP(1) - 1
120 IF KY$ = "S" THEN PP(1) = PP(1) + 1
130 IF PP(1) < 1 THEN PP(1) = 1
140 IF PP(1) > LH THEN PP(1) = LH
150 IF KY$ = "A" THEN PP(2) = PP(2) - 1
160 IF KY$ = "D" THEN PP(2) = PP(2) + 1
170 IF PP(2) < 1 THEN PP(2) = 1
180 IF PP(2) > LW THEN PP(2) = LW
185 IF KY$ = "R" THEN GOSUB 2000
190 PRINT PP(1) ;
200 PRINT "," ;
210 PRINT PP(2)

0998 GOSUB 3000
0999 GOTO 0090

1000 REM LOAD NEXT LEVEL
1001 READ LH : REM LOAD LEVEL HEIGHT INTO MEMORY
1002 READ LW : REM LOAD LEVEL WIDTH INTO MEMORY 
1010 FOR LY = 1 TO LH : REM LEVEL Y
1020 FOR LX = 1 TO LW : REM LEVEL X
1030 READ CELL
1040 LA(LY, LX) = CELL
1060 NEXT LX
1070 NEXT LY
1080 RETURN

2000 REM RESET GAME WORLD TO LEVEL
2010 FOR WY = 1 TO LH : REM WORLD Y
2020 FOR WX = 1 TO LW : REM WORLD X
2040 WA(WY, WX) = LA(WY, WX)
2050 IF WA(WY, WX) = 1 THEN PP(1) = WY: PP(2) = WX: WA(WY, WX) = 0
2060 NEXT WX
2070 NEXT WY
2080 RETURN

3000 REM RENDER WORLD
3020 FOR WY = 1 TO LH
3030 FOR WX = 1 TO LW
3050 REM BIT 0 = PLAYER, 1 = WALL, 2 = BOX, 3 = GOAL
3060 CELL = WA(WY, WX)
3070 COLOR = 5 : REM FLOOR COLOR
3080 IF CELL = 8 THEN COLOR = 9 : REM GOAL COLOR
3090 IF CELL = 2 THEN COLOR = 1 : REM WALL COLOR
3100 IF CELL = 4 THEN COLOR = 8 : REM BOX COLOR
3120 IF WY = PP(1) AND WX = PP(2) THEN COLOR = 2 : REM PLAYER COLOR
3130 PLOT WX,WY * 2
3140 PLOT WX,WY * 2 + 1
3150 NEXT WX
3160 NEXT WY
3170 RETURN

60000 DATA 8,8
60001 DATA 2,2,2,2,2,2,2,2
60010 DATA 2,1,2,2,2,2,2,2
60020 DATA 2,0,4,0,0,8,0,2
60030 DATA 2,0,0,0,0,0,0,2
60040 DATA 2,2,0,0,2,2,0,2
60050 DATA 2,2,0,4,0,0,8,2
60060 DATA 2,2,0,0,0,2,0,2
60070 DATA 2,2,2,2,2,2,2,2
